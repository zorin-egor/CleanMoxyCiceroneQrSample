import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
//apply plugin: 'com.google.firebase.crashlytics'

android {
    namespace = "com.sample.qr"
    flavorDimensions = [FLAVOR_DIM]

    signingConfigs {
        signed {
            keyAlias 'Kotlin'
            keyPassword 'KotlinPWD'
            storeFile file('../Kotlin.jks')
            storePassword 'KotlinPWD'
        }
    }

    defaultConfig {
        applicationId APP_ID
        minSdkVersion SDK_MIN
        targetSdkVersion SDK_MAX
        compileSdk SDK_MAX
        buildToolsVersion = BUILD_TOOL
        versionCode APP_CODE
        versionName APP_VERSION
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    productFlavors {
        dev {
            dimension FLAVOR_DIM
            applicationId "${APP_ID}.beta"
            versionName "${APP_VERSION}b"
        }

        prod {
            dimension FLAVOR_DIM
            applicationId APP_ID
            versionName APP_VERSION
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.signed
        }

        debug {
            signingConfig signingConfigs.signed
            minifyEnabled = false
            debuggable true
        }

        applicationVariants.all { variant ->
            variant.outputs.all {
                def buildType = variant.buildType.isDebuggable()? '_debug' : ''
                def dateTime =  new SimpleDateFormat("_MMMMM.dd_HH-mm").format(new Date())
                outputFileName = "Talents_${variant.versionName}${buildType}${dateTime}_build-${variant.versionCode}.apk"
            }
        }
    }

    preBuild.doLast {
        delete fileTree(buildDir) {
            include '**/*.apk'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    buildFeatures {
        dataBinding = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(":domain")
    implementation project(":data")
    implementation project(":presentation")

    implementation platform("com.google.firebase:firebase-bom:$GOOGLE_BOM")
//    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$KOTLIN"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}